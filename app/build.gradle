plugins {
    id "com.android.application"
}

android {
    compileSdkVersion 34
    namespace "de.robv.android.xposed.installer"

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        applicationId "de.robv.android.xposed.installer"
        resConfigs "en"
        minSdkVersion 15
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 25
        versionCode 43
        versionName "3.1.5"
        archivesBaseName = "XposedInstaller"
    }

    buildTypes {
        debug {
            pseudoLocalesEnabled true
        }
    }

    if (System.getenv("RELEASE_KEYSTORE_FILE")) {
        signingConfigs {
            release {
                storeFile file(System.getenv("RELEASE_KEYSTORE_FILE"))
                storePassword System.getenv("RELEASE_KEYSTORE_PASSWORD")
                keyAlias System.getenv("RELEASE_KEYSTORE_ALIAS")
                keyPassword System.getenv("RELEASE_KEYSTORE_PASSWORD")
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
                minifyEnabled true
                proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            }
        }
    }
}

dependencies {
    implementation libs.support.cardview
    implementation libs.support.design
    implementation libs.support.customtabs
    implementation libs.support.support.v13
    implementation libs.licensesdialog
    implementation libs.materialdialogs
    compileOnly fileTree(dir: "libs", include: ["*.jar"])
}
